apply from: "../commons-device.gradle"
apply plugin: 'kotlin-android'

/* gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

android {
    defaultConfig {
        applicationId "org.torproject.android"
        versionName getVersionName()
        minSdkVersion 23
        compileSdk 34
        targetSdkVersion 34
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        archivesBaseName = "Orbot"
    }
    compileOptions {
//        coreLibraryDesugaringEnabled true
    }
    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    flavorDimensions "free"

    productFlavors {
        fullperm {
            dimension "free"
            applicationId 'org.torproject.android'
            versionCode 1732300100
            versionName getVersionName()
            archivesBaseName = "Orbot-${versionName}"
        }

        nightly {
            dimension "free"
            applicationId 'org.torproject.android.nightly'
            versionName getVersionName()
            versionCode versionCodeEpoch()
            archivesBaseName = "Orbot-${versionName}"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/androidx.localbroadcastmanager_localbroadcastmanager.version']
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'InvalidPackage'
        htmlReport true
        lintConfig file('../lint.xml')
        textReport false
        xmlReport false
    }
    namespace 'org.torproject.android'
}
dependencies {
    // Use locally built ipt_proxy+go_tun2socks
    implementation(project(':OrbotLib'))
    implementation(project(':appcore'))
    implementation(project(':intentintegrator'))
    implementation(project(':orbotservice'))

    // Core
    implementation(libs.androidx.core.ktx)
    implementation(libs.kotlin.stdlib.jdk8)

    // Kotlin
    implementation(libs.androidx.navigation.fragment.ktx)
    implementation(libs.androidx.navigation.ui.ktx)
    implementation(libs.android.material)
    implementation(libs.android.volley)
    implementation(libs.androidx.localbroadcast)

    // Root detection
    implementation(libs.rootbeer.lib)

    // Retrofit
    implementation(libs.retrofit)
    implementation(libs.converter.gson)

    // Testing
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.fastlane.screengrab)
}
