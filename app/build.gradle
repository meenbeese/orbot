apply from: '../dependencies.gradle'
apply from: '../commons.gradle'
apply plugin: 'kotlin-android'

/* Gets the version name from the latest Git tag, stripping the leading v off */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

android {
    compileSdk 33
    defaultConfig {
        applicationId 'org.torproject.android'
        versionName getVersionName()
        minSdk 23
        targetSdk 33
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        archivesBaseName = 'Orbot'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    flavorDimensions 'free'

    productFlavors {
        fullperm {
            dimension 'free'
            applicationId 'org.torproject.android'
            versionCode 1700200500
            versionName getVersionName()
            archivesBaseName = 'Orbot-${versionName}'
        }

        nightly {
            dimension 'free'
            applicationId 'org.torproject.android.nightly'
            versionName getVersionName()
            versionCode versionCodeEpoch()
            archivesBaseName = 'Orbot-${versionName}'
        }
    }

    dependencies {
        /* Orbot Libraries */
        implementation project(':appcore')
        implementation project(':intentintegrator')
        implementation project(':orbotservice')
        implementation project(':OrbotLib')

        /* Android Core */
        implementation libs.androidx_appcompat
        implementation libs.androidx_core_ktx
        implementation libs.androidx_constraint
        implementation libs.androidx_coordinator
        implementation libs.androidx_leanback_core
        implementation libs.androidx_leanback_paging
        implementation libs.androidx_leanback_preferences
        implementation libs.androidx_leanback_tab
        implementation libs.androidx_localbroadcast
        implementation libs.androidx_palette
        implementation libs.androidx_recyclerview
        implementation libs.androidx_navigation_fragment
        implementation libs.androidx_navigation_ui

        /* Design */
        implementation libs.android_material
        implementation libs.android_snowfall
        implementation libs.android_volley

        /* HTTP Client */
        implementation libs.squareup_gson
        implementation libs.squareup_retrofit

        /* Testing */
        implementation libs.apl_appintro
        implementation libs.junit_junit
        androidTestImplementation libs.androidx_test_espresso
        androidTestImplementation libs.androidx_test_junit
        androidTestImplementation libs.fastlane_screengrab
    }
    namespace 'org.torproject.android'
}